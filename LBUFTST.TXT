       DEF  LTEST
       REF  LOADER,VMBW

* Use this program in order to load all
* of the files needed to test SCRNWRT.
* It will execute the SCRNWRT tests
* automatically.

PABBUF EQU  >1000
PAB    EQU  >F80
STATUS EQU  >837C
PNTR   EQU  >8356
* Byte 0 = Open
* Byte 1 = Status/Display/Fixed
* Byte 4 = max record length 80
* Byte 5 = actual length to write
* Byte 6-7 are not relevant
* Byte 8 = status o file
* Byte 9 = file name length
PDATA1 DATA >0004,PABBUF,>5000,>0000,>000D
FILEN1 TEXT 'DSK2.BUFTST.O  '
       BSS  7
PDATA2 DATA >0004,PABBUF,>5000,>0000,>000F
FILEN2 TEXT 'DSK2.TESTUTIL.O'
       BSS  7
PDATA3 DATA >0004,PABBUF,>5000,>0000,>000D
FILEN3 TEXT 'DSK2.MEMBUF.O  '
       BSS  7
PDATAE

LDMSG  TEXT 'Loading DSK2.BUFTST.O,          '
       TEXT 'DST2.TESTUTIL.O, DSK2.MEMBUF.O  '

LTEST  LI   R0,0
       LI   R1,LDMSG
       LI   R2,>40
       BLWP @VMBW
* Write PAB to VDP
       LI   R0,PAB
       LI   R1,PDATA1
       LI   R2,>20
LTEST1 BLWP @VMBW
       LI   R6,PAB+9
       MOV  R6,@PNTR
* Load the assembled code
       BLWP @LOADER
* If the Eqaul bit is set, report error
       JEQ  RPTERR
* Update R1 so we can load the next file
       A    R2,R1
       CI   R1,PDATAE
       JL   LTEST1
* Enter the test program
       B    @RUNTST
*
*
ERRMSG TEXT 'LOADER routine error: '
ERRCD  BSS  1
ERRCD1 EVEN
ERRFL  TEXT 'File: '
ERRFL1
*
RPTERR MOV  R1,R3
* Add error code to message
       AI   R0,>3000
       MOVB R0,@ERRCD
* Display error message
       LI   R0,>60
       LI   R1,ERRMSG
       LI   R2,ERRCD1-ERRMSG
       BLWP @VMBW
* Display particular file to fail
       LI   R0,>80
       LI   R1,ERRFL
       LI   R2,ERRFL1-ERRFL
       BLWP @VMBW
       A    R2,R0
       MOV  R3,R1
       AI   R1,>A
       LI   R2,>10
       BLWP @VMBW
JMP    JMP  JMP
*
* First loaded program will be found here:
RUNTST
       END