       DEF  TSTLST,RSLTFL
       REF  AEQ,ABLCK
*
       REF  BUFINT,BUFCPY,BUFALC,BUFREE
       REF  BUFLES
*
       REF  BUFADR,BUFEND

TSTLST DATA TSTEND-TSTLST-2/8
* Copy memory block. Non-overlapping.
       DATA TCPY1
       TEXT 'TCPY1 '
* Copy memory block. Length is zero.
       DATA TCPY2
       TEXT 'TCPY2 '
* Copy memory block. Overlapping.
       DATA TCPY3
       TEXT 'TCPY3 '
* Copy memory block. Overlapping.
       DATA TCPY4
       TEXT 'TCPY4 '
* Initialize a >1000 bytes buffer.
       DATA TINT1
       TEXT 'TINT1 '
* Fail to initialize with >8000 bytes.
       DATA TINT2
       TEXT 'TINT2 '
* Fail to initialize with >2 bytes.
       DATA TINT3
       TEXT 'TINT3 '
* Fail to initialize with >13 bytes.
       DATA TINT4
       TEXT 'TINT4 '
* Allocate space in empty buffer.
       DATA TALC1
       TEXT 'TALC1 '
* Allocate space following another
*    allocation.
       DATA TALC2
       TEXT 'TALC2 '
* Allocate space between two allocated
*    spaces.
       DATA TALC3
       TEXT 'TALC3 '
* Allocate space between two allocated
*    spaces, leave some unallocated
*    remaining.
       DATA TALC4
       TEXT 'TALC4 '
* Allocate space.
*    Earlier empty space too small.
       DATA TALC5
       TEXT 'TALC5 '
* Allocate last remaining space in buffer.
       DATA TALC6
       TEXT 'TALC6 '
* No unallocated space is large enough.
       DATA TALC7
       TEXT 'TALC7 '

* Deallocate only allocated space
* Deallocate last of several allocated
*    spaces
* Deallocate space at end of buffer
* Deallocate space between two allocated
*    spaces
* Deallocate space after allocated but
*    before deallocated space
TSTEND
RSLTFL BYTE RSLTFE-RSLTFL-1
       TEXT 'DSK2.TESTRESULT.TXT'
RSLTFE
       EVEN
*
SPACE  BSS  >400

*
* Copy memory block. Non-overlapping.
TCPY1
* Act
       LI   R0,TCPY1A
       LI   R1,TCPY1C
       LI   R2,TCPY1B-TCPY1A
       BLWP @BUFCPY
* Assert the data was copied correctly.
       LI   R0,TCPY1A
       LI   R1,TCPY1C
       LI   R2,TCPY1B-TCPY1A
       BLWP @ABLCK
       TEXT 'Copied string should match original.'
       BYTE 0
       EVEN
*
       RT
TCPY1A TEXT 'Some data to copy.'
TCPY1B EVEN
       BSS  >10
TCPY1C BSS  >20

*
* Copy memory block. Length is zero.
TCPY2
* Act
       LI   R0,TCPY2A
       LI   R1,TCPY2C
       LI   R2,0
       BLWP @BUFCPY
* No real assertion.
* If this test fails, it will probably
* mean an infinate loop.
       RT
TCPY2A BSS  >2
TCPY2C BSS  >2

*
* Copy memory block. Overlapping.
* Source block is earlier than
* destination block.
TCPY3
* Act
       LI   R0,TCPY3B
       LI   R1,TCPY3D
       LI   R2,TCPY3C-TCPY3B
       BLWP @BUFCPY
* Assert
       LI   R0,TCPY3A
       LI   R1,TCPY3D
       LI   R2,TCPY3B-TCPY3A
       BLWP @ABLCK
       TEXT 'Copied string should match original.'
       BYTE 0
       EVEN
*
       RT
* Expected text
TCPY3A TEXT 'This is some data that may be copied._'
       EVEN
* Source to copy from
TCPY3B TEXT 'This is some data that may be copied._'
       EVEN
TCPY3C BSS  >20
TCPY3D EQU  TCPY3B+>8

*
* Copy memory block. Overlapping.
* Source block is later than
* destination block.
TCPY4
* Act
       LI   R0,TCPY4C
       LI   R1,TCPY4E
       LI   R2,TCPY4D-TCPY4C
       BLWP @BUFCPY
* Assert
       LI   R0,TCPY4A
       LI   R1,TCPY4E
       LI   R2,TCPY4B-TCPY4A
       BLWP @ABLCK
       TEXT 'Copied string should match original.'
       BYTE 0
       EVEN
*
       RT
* Expected text
TCPY4A TEXT 'This is some data that may be copied._'
TCPY4B EVEN
       BSS  >20
* Source to copy from
TCPY4C TEXT 'This is some data that may be copied._'
TCPY4D
TCPY4E EQU  TCPY4C->8
       EVEN

*
* Initialize a >1000 bytes buffer.
* Expect the first word to contain the
* header for one empty chunk covering
* the entire buffer.
TINT1
* Act
       LI   R0,SPACE
       LI   R1,>200
       BLWP @BUFINT
*
       CLR  R3
* Assert R0 = zero (no error)
       MOV  R0,R1
       CLR  R0
       BLWP @AEQ
       TEXT 'There should be no allocation error.'
       BYTE 0
       EVEN
* Assert R0 = zero (no error)
       LI   R0,>200
       MOV  @SPACE,R1
       BLWP @AEQ
       TEXT 'The first word should contain >0200.'
       BYTE 0
       EVEN
*
       RT
 
*
* Fail to initialize with >8000 bytes.
* Expect the allocation to fail because
* >8000 is too big.
TINT2
* Act
       LI   R0,SPACE
       LI   R1,>8000
       BLWP @BUFINT
* Assert R0 = >FFFF (error)
       CLR  R3
       MOV  R0,R1
       SETO R0
       BLWP @AEQ
       TEXT 'Allocation should fail. '
       TEXT '>8000 is too big.'
       BYTE 0
       EVEN
*
       RT

*
* Fail to initialize with >2 bytes.
* Expect the allocation to fail because
* >0002 is too small.
TINT3
* Act
       LI   R0,SPACE
       LI   R1,>2
       BLWP @BUFINT
* Assert R0 = >FFFF (error)
       CLR  R3
       MOV  R0,R1
       SETO R0
       BLWP @AEQ
       TEXT 'Allocation should fail. '
       TEXT '>03 is too small.'
       BYTE 0
       EVEN
*
       RT

*
* Fail to initialize with >13 bytes.
* Expect the allocation to fail because
* space must be even.
TINT4
* Act
       LI   R0,SPACE
       LI   R1,>13
       BLWP @BUFINT
* Assert R0 = >FFFF (error)
       CLR  R3
       MOV  R0,R1
       SETO R0
       BLWP @AEQ
       TEXT 'Allocation should fail. '
       TEXT 'Allocated space must be even.'
       BYTE 0
       EVEN
*
       RT

*
* Allocate space in empty buffer.
TALC1
* Arrange
       LI   R0,TALC1A
       MOV  R0,@BUFADR
       LI   R0,TALC1B
       MOV  R0,@BUFEND
* Act
       LI   R0,>E
       BLWP @BUFALC
* Assert
       CLR  R3
* >10 bytes should be allocated
* (>E bytes + header)
       LI   R0,>8010
       MOV  @TALC1A,R1
       BLWP @AEQ
       TEXT 'Space should now be allocated.'
       BYTE 0
       EVEN
* >30 bytes remain unallocated
       LI   R0,>0030
       MOV  @TALC1A+>10,R1
       BLWP @AEQ
       TEXT 'Space should remain unallocated.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC1A
* Header
       DATA >0040
* Allocated space
       BSS  >3E
TALC1B

* Allocate space following another
*    allocation.
TALC2
* Arrange
       LI   R0,TALC2A
       MOV  R0,@BUFADR
       LI   R0,TALC2B
       MOV  R0,@BUFEND
* Act
       LI   R0,>C
       BLWP @BUFALC
* Assert
       CLR  R3
*
       LI   R0,>8008
       MOV  @TALC2A,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
* >E bytes should be allocated
* (>C bytes + header)
       LI   R0,>800E
       MOV  @TALC2A+>20,R1
       BLWP @AEQ
       TEXT 'Space should now be allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>8018
       MOV  @TALC2A+>8,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>0012
       MOV  @TALC2A+>2E,R1
       BLWP @AEQ
       TEXT 'Space should remain unallocated.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC2A DATA >8008
       BSS  >06
       DATA >8018
       BSS  >16
       DATA >0020
       BSS  >1E
TALC2B

* Allocate space between two allocated
*    spaces.
TALC3
* Arrange
       LI   R0,TALC3A
       MOV  R0,@BUFADR
       LI   R0,TALC3B
       MOV  R0,@BUFEND
* Act
       LI   R0,>16
       BLWP @BUFALC
* Assert
       CLR  R3
*
       LI   R0,>8008
       MOV  @TALC3A,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
* >18 bytes should be allocated
* (>16 bytes + header)
       LI   R0,>8018
       MOV  @TALC3A+>08,R1
       BLWP @AEQ
       TEXT 'Space should now be allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>8010
       MOV  @TALC3A+>20,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
* >10 bytes remain unallocated
       LI   R0,>0010
       MOV  @TALC3A+>30,R1
       BLWP @AEQ
       TEXT 'Space should remain unallocated.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC3A DATA >8008
       BSS  >06
       DATA >0018
       BSS  >16
       DATA >8010
       BSS  >0E
       DATA >0010
       BSS  >0E
TALC3B

* Allocate space between two allocated
*    spaces, leave some unallocated
*    remaining.
TALC4
* Arrange
       LI   R0,TALC4A
       MOV  R0,@BUFADR
       LI   R0,TALC4B
       MOV  R0,@BUFEND
* Act
       LI   R0,>14
       BLWP @BUFALC
* Assert
       CLR  R3
* >08 bytes remain allocated
       LI   R0,>8008
       MOV  @TALC4A,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
* >16 bytes should be allocated
* (>14 bytes + header)
       LI   R0,>8016
       MOV  @TALC4A+>08,R1
       BLWP @AEQ
       TEXT 'Space should now be allocated.'
       BYTE 0
       EVEN
* >02 bytes remain unallocated
       LI   R0,>0002
       MOV  @TALC4A+>1E,R1
       BLWP @AEQ
       TEXT 'Space should remain unallocated.'
       BYTE 0
       EVEN
* >10 bytes remain allocated
       LI   R0,>8010
       MOV  @TALC4A+>20,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
* >10 bytes remain unallocated
       LI   R0,>0010
       MOV  @TALC4A+>30,R1
       BLWP @AEQ
       TEXT 'Space should remain unallocated.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC4A DATA >8008
       BSS  >06
       DATA >0018
       BSS  >16
       DATA >8010
       BSS  >0E
       DATA >0010
       BSS  >0E
TALC4B

*
* Allocate space.
*    Earlier empty space too small.
TALC5
* Arrange
       LI   R0,TALC5A
       MOV  R0,@BUFADR
       LI   R0,TALC5B
       MOV  R0,@BUFEND
* Act
       LI   R0,>E
       BLWP @BUFALC
* Assert
       LI   R0,>8008
       MOV  @TALC5A,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>0004
       MOV  @TALC5A+>08,R1
       BLWP @AEQ
       TEXT 'Space should remain unallocated.'
       BYTE 0
       EVEN
*
       LI   R0,>8004
       MOV  @TALC5A+>0C,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>8020
       MOV  @TALC5A+>10,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>8010
       MOV  @TALC5A+>30,R1
       BLWP @AEQ
       TEXT 'Space should now be allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>0008
       MOV  @TALC5A+>40,R1
       BLWP @AEQ
       TEXT 'A little bit of unallocated space left over.'
       BYTE 0
       EVEN
*
       LI   R0,>8010
       MOV  @TALC5A+>48,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC5A DATA >8008
       BSS  >06
       DATA >0004
       BSS  >2
       DATA >8004
       BSS  >2
       DATA >8020
       BSS  >1E
       DATA >0018
       BSS  >16
       DATA >8010
       BSS  >E
TALC5B

*
* Allocate last remaining space in buffer.
TALC6
* Arrange
       LI   R0,TALC6A
       MOV  R0,@BUFADR
       LI   R0,TALC6B
       MOV  R0,@BUFEND
* Act
       LI   R0,>1C
       BLWP @BUFALC
* Assert
       LI   R0,>8016
       MOV  @TALC6A,R1
       BLWP @AEQ
       TEXT 'Space should remain allocated.'
       BYTE 0
       EVEN
*
       LI   R0,>801E
       MOV  @TALC6A+>16,R1
       BLWP @AEQ
       TEXT 'The last bit of space should now be allocated.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC6A DATA >8016
       BSS  >14
       DATA >001E
       BSS  >1C
TALC6B

*
* No unallocated space is large enough.
TALC7
* Arrange
       LI   R0,TALC7A
       MOV  R0,@BUFADR
       LI   R0,TALC7B
       MOV  R0,@BUFEND
* Act
       LI   R0,>12
       BLWP @BUFALC
* Assert
       MOV  R0,R1
       SETO R0
       BLWP @AEQ
       TEXT 'An error should be reported. '
       TEXT 'There is not enough space.'
       BYTE 0
       EVEN
*
       RT
* Starting buffer
TALC7A DATA >8016
       BSS  >14
       DATA >0010
       BSS  >0E
       DATA >801C
       BSS  >1A
TALC7B

       END