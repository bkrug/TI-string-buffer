       DEF  TSTLST,RSLTFL
       REF  AEQ,ABLCK
*
       REF  BUFINT,BUFCPY,BUFALC,BUFREE
       REF  BUFLES

TSTLST DATA TSTEND-TSTLST-2/8
* Copy memory block. Non-overlapping.
       DATA TCPY1
       TEXT 'TCPY1 '
* Copy memory block. Length is zero.
       DATA TCPY2
       TEXT 'TCPY2 '
* Copy memory block. Overlapping.
       DATA TCPY3
       TEXT 'TCPY3 '
* Copy memory block. Overlapping.
       DATA TCPY4
       TEXT 'TCPY4 '
* Initialize a >1000 bytes buffer.
       DATA TINT1
       TEXT 'TINT1 '
* Fail to initialize with >8000 bytes.
       DATA TINT2
       TEXT 'TINT2 '
* Fail to initialize with >2 bytes.
       DATA TINT3
       TEXT 'TINT3 '
* Fail to initialize with >13 bytes.
       DATA TINT4
       TEXT 'TINT4 '
TSTEND
RSLTFL BYTE RSLTFE-RSLTFL-1
       TEXT 'DSK2.TESTRESULT.TXT'
RSLTFE
       EVEN
*
SPACE  BSS  >1800

*
* Copy memory block. Non-overlapping.
TCPY1
* Act
       LI   R0,TCPY1A
	   LI   R1,TCPY1C
	   LI   R2,TCPY1B-TCPY1A
	   BLWP @BUFCPY
* Assert the data was copied correctly.
       LI   R0,TCPY1A
	   LI   R1,TCPY1C
	   LI   R2,TCPY1B-TCPY1A
	   LI   R3,TCPY1D
	   LI   R4,TCPY1E-TCPY1D
	   BLWP @ABLCK
*
       RT
TCPY1A TEXT 'Some data to copy.'
TCPY1B EVEN
       BSS  >10
TCPY1C BSS  >20
TCPY1D TEXT 'Copied string should match original.'
TCPY1E

*
* Copy memory block. Length is zero.
TCPY2
* Act
       LI   R0,TCPY2A
	   LI   R1,TCPY2C
	   LI   R2,0
	   BLWP @BUFCPY
* No real assertion.
* If this test fails, it will probably
* mean an infinate loop.
       RT
TCPY2A BSS  >2
TCPY2C BSS  >2

*
* Copy memory block. Overlapping.
* Source block is earlier than
* destination block.
TCPY3
* Act
       LI   R0,TCPY3B
	   LI   R1,TCPY3D
	   LI   R2,TCPY3C-TCPY3B
	   BLWP @BUFCPY
* Assert
       LI   R0,TCPY3A
	   LI   R1,TCPY3D
	   LI   R2,TCPY3B-TCPY3A
	   LI   R3,TCPY1D
	   LI   R4,TCPY1E-TCPY1D
	   BLWP @ABLCK
*
       RT
* Expected text
TCPY3A TEXT 'This is some data that may be copied._'
       EVEN
* Source to copy from
TCPY3B TEXT 'This is some data that may be copied._'
       EVEN
TCPY3C BSS  >20
TCPY3D EQU  TCPY3B+>8

*
* Copy memory block. Overlapping.
* Source block is later than
* destination block.
TCPY4
* Act
       LI   R0,TCPY4C
	   LI   R1,TCPY4E
	   LI   R2,TCPY4D-TCPY4C
	   BLWP @BUFCPY
* Assert
       LI   R0,TCPY4A
	   LI   R1,TCPY4E
	   LI   R2,TCPY4B-TCPY4A
	   LI   R3,TCPY1D
	   LI   R4,TCPY1E-TCPY1D
	   BLWP @ABLCK
*
       RT
* Expected text
TCPY4A TEXT 'This is some data that may be copied._'
TCPY4B EVEN
       BSS  >20
* Source to copy from
TCPY4C TEXT 'This is some data that may be copied._'
TCPY4D
TCPY4E EQU  TCPY4C->8
       EVEN

*
* Initialize a >1000 bytes buffer.
* Expect the first word to contain the
* header for one empty chunk covering
* the entire buffer.
TINT1
* Act
       LI   R0,SPACE
       LI   R1,>1000
       BLWP @BUFINT
* Assert R0 = zero (no error)
       MOV  R0,R1
       CLR  R0
       LI   R2,TINT1A
       LI   R3,TINT1B-TINT1A
       BLWP @AEQ
* Assert R0 = zero (no error)
       LI   R0,>1000
       LI   R1,SPACE
       MOV  *R1,R1
       LI   R2,TINT1B
       LI   R3,TINT1C-TINT1B
       BLWP @AEQ       
*
       RT
TINT1A TEXT 'There should be no allocation error.'
TINT1B TEXT 'The first word should contain >1000.'
TINT1C
 
*
* Fail to initialize with >8000 bytes.
* Expect the allocation to fail because
* >8000 is too big.
TINT2
* Act
       LI   R0,SPACE
       LI   R1,>8000
       BLWP @BUFINT
* Assert R0 = >FFFF (error)
       MOV  R0,R1
       SETO R0
       LI   R2,TINT2A
       LI   R3,TINT2B-TINT2A
       BLWP @AEQ
*
       RT
TINT2A TEXT 'Allocation should fail. '
       TEXT '>8000 is too big.'
TINT2B

*
* Fail to initialize with >2 bytes.
* Expect the allocation to fail because
* >0002 is too small.
TINT3  LI   R0,SPACE
       LI   R1,>2
       BLWP @BUFINT
* Assert R0 = >FFFF (error)
       MOV  R0,R1
       SETO R0
       LI   R2,TINT3A
       LI   R3,TINT3B-TINT3A
       BLWP @AEQ
*
       RT
TINT3A TEXT 'Allocation should fail. '
       TEXT '>3 is too small.'
TINT3B

*
* Fail to initialize with >13 bytes.
* Expect the allocation to fail because
* space must be even.
TINT4
* Act
       LI   R0,SPACE
       LI   R1,>13
       BLWP @BUFINT
* Assert R0 = >FFFF (error)
       MOV  R0,R1
       SETO R0
       LI   R2,TINT4A
       LI   R3,TINT4B-TINT4A
       BLWP @AEQ
*
       RT
TINT4A TEXT 'Allocation should fail. '
       TEXT 'Allocated space must be even.'
TINT4B

       END